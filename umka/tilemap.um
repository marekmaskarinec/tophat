
import (
	"csv.um"
	"image.um"
	"std.um"
	"rectangle.um"
	"entity.um"
	"polygon.um"
)

type tmap* = struct {
	tiles: []image.img
	x: int
	y: int
	w: int
	cells: []int32
	collmask: []bool
	cellsize: int
	scaletype: int
}

type ctmap* = struct {
	tiles: ^int
	x: int32
	y: int32
	w: int32
	h: int32
	cells: ^int32
	collmask: ^int32
	cellsize: int32
	scaletype: int32
}

const (
	STRETCH* = 0
	TOPLEFT* = 1
)

fn parse_num(inp: char): (int, bool) {
	if inp >= '0' && inp <= '9' {
		return int(inp) - int('0'), true
	}

	if inp >= 'A' && inp <= 'Z' {
		return int(inp) - int('A'), true
	}

	if inp >= 'a' && inp <= 'z' {
		return int(inp) - int('a'), true
	}

	return 0, false
}

fn mk*(inp: str, images: []image.img): tmap {
	res := csv.parse(inp)

	if len(res) < 2 { return tmap{} }
	if len(res[0]) < 2 { return tmap{} }
	if res[0][0] != "tophat tilemap" { return tmap{} }

	if len(res[1]) != len(images) {
		error("incorrect collision mask in tilemap")
	}
	collmask := make([]bool, len(res[1]))
	for i:=0; i < len(res[1]); i++ {
		if res[1][i] == "true" { collmask[i] = true; continue }
		collmask[i] = false
	}

	cells := []int32{}
	for i:=2; i < len(res); i++ {
		for j:=0; j < len(res[i]); j++ {
			if res[i][j] == "" {
				continue
			}

			val, check := parse_num(res[i][j][0])
			if !check {
				error("number in tilemap isn't valid")
			}

			if val > len(images) {
				error("number on " + repr(i) + ", " + repr(j) + "exceeds number of images")
			}

			cells = append(cells, val)
		}
	}

	return tmap{images, std.atoi(res[0][1]), std.atoi(res[0][2]), len(res[2]), cells, collmask, std.atoi(res[0][3]), std.atoi(res[0][4])}
}

fn (t: ^tmap) edit*(x, y, tile: int) { t.cells[y*t.w + x] = tile }

fn cdrawtmap(ct: ^ctmap, cam: ^rectangle.rect)

fn (t: ^tmap) draw*(cam: rectangle.rect) {
	if len(t.cells) == 0 { return }

	ccm := make([]int32, len(t.collmask))
	for i:=0; i < len(t.collmask); i++ {
		ccm[i] = int32(t.collmask[i])
	}

	cm := ctmap{^int(&t.tiles[0]), t.x, t.y, t.w, len(t.cells)/t.w, &t.cells[0], &ccm[0], t.cellsize, t.scaletype}
	cdrawtmap(&cm, &cam)
}

fn ctmapgetcoll(tx, ty, x, y: ^int32, t: ^ctmap, e: ^entity.cent): int32

fn (t: ^tmap) getcoll*(e: entity.ent, rx, ry, tx, ty: ^int32): bool {
	if len(t.cells) == 0 { return false }

	ccm := make([]int32, len(t.collmask))
	for i:=0; i < len(t.collmask); i++ {
		ccm[i] = int32(t.collmask[i])
	}

	cm := ctmap{^int(&t.tiles[0]), t.x, t.y, t.w, len(t.cells)/t.w, &t.cells[0], &ccm[0], t.cellsize, t.scaletype}
	
	cp := polygon.cpoly{round(e.p.x), round(e.p.y), &e.p.v[0], e.p.w, e.p.h, e.p.vc}
	ce := entity.cent{&cp, e.image, e.sx, e.sy, round(e.rot), e.color, e.id}

	c := ctmapgetcoll(tx, ty, rx, ry, &cm, &ce)

	return bool(c)
}
