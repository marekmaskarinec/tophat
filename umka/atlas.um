
import (
	"th.um"
	"image.um"
)

// Atlas is an image containing tiles in a square grid.
type Atlas* = struct {
	i: image.Image // source image
	cs: th.Vf2 // size of a cell in pixels
	dm: th.Vf2 // amount of cells in image
}

// i: source image
// dm: amount of cells
fn mk*(i: image.Image, dm: th.Vf2): Atlas {
	a := Atlas{}

	a.i = i
	a.dm = dm
	a.cs = i.getDims().div(dm)

	return a
}

// returns the coordinates of the nth tile
fn (a: ^Atlas) coords*(n: int): th.Vf2 {
	return th.Vf2{ n % trunc(a.dm.x), (n - n % trunc(a.dm.x)) / a.dm.x }
}

// Crops the sourse image to only show a wanted tile
fn (a: ^Atlas) cropSource*(at: th.Vf2) {
	if (at.x > a.dm.x || at.y > a.dm.y || at.x * at.y < 0) {
		return
	}

	a.i.crop(
		th.Vf2{at.x / a.dm.x, at.y / a.dm.y},
		th.Vf2{(at.x+1) / a.dm.x, (at.y+1) / a.dm.y})
}

// Draws the tile at `at`
fn (a: ^Atlas) draw*(at: th.Vf2, t: th.Transform) {
	a.cropSource(at)
	a.i.draw(t, th.white)
	a.i.crop(th.Vf2{}, th.Vf2{1, 1})
}
