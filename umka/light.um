// ligthing system

import (
	"th.um"
	"rectangle.um"
)

// Mask is a set of dots that can be edited and drawn to emulate light.
// You can even edit the dots yourself, but it won't look good with multiple
// lights. A function for plotting a pixel will be available soon.
type mask* = struct {
	dots: []uint32
	w, h: th.uu
	rectsize: th.fu
	color: uint32
}

type cmask = struct {
	dots: ^uint32
	w, h: th.uu
	rectsize: th.fu
	color: uint32
}

// A circular light that can be plotted onto a mask.
type spotlight* = struct {
	pos: th.vf2
	radius: th.uu
	tint: uint32
}

fn clightmaskclear(m: ^cmask)

// Clears the lightmask to have it's own default color.
fn (m: ^mask) clear*() {
	cm := cmask {
		&m.dots[0],
		m.w, m.h, m.rectsize,
		m.color}

	clightmaskclear(&cm)
}

fn mk*(color: uint32, rectsize: real, cam: rectangle.rect): mask {
	d := mask{}

	d.rectsize = rectsize
	d.w = round(cam.w / rectsize)
	d.h = round(cam.h / rectsize)
	d.dots = make([]uint32, d.w * d.h)
	d.color = color

	d.clear()

	return d
}

fn clightmaskdraw(m: ^cmask, cam: ^rectangle.rect)

// Draws a lightmask.
fn (m: ^mask) draw*(cam: rectangle.rect) {
	cm := cmask {
		&m.dots[0],
		m.w, m.h, m.rectsize,
		m.color}

	clightmaskdraw(&cm, &cam)
}

fn cspotlightstamp(s: ^spotlight, m: ^cmask, cam: ^rectangle.rect)

// Stamps a spotlight onto a mask.
fn (s: ^spotlight) stamp*(m: ^mask, cam: rectangle.rect) {
	cm := cmask {
		&m.dots[0],
		m.w, m.h, m.rectsize,
		m.color}

	cspotlightstamp(s, &cm, &cam)
}

