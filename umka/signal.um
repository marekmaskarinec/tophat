// A module for importless communication between modules. A signal is an array
// of callbacks with a name.

import (
	"map.um"
)

type Any* = interface{}
type Callback* = fn(ctx: Any) 
type Signal* = []Callback

var signals: map.Map

// Registers a callback to a signal. There is no need to explicitely create
// signals.
fn register*(name: str, callback: Callback) {
	ptr := signals.get(name)

	arr := ^Signal(ptr)
	if arr == null {
		signals.set(name, []Callback{})
		arr = ^Signal(signals.get(name))
	}

	arr^ = append(arr^, callback)
}

// Calls all callbacks associated with the passed name.
fn emit*(name: str, ctx: Any) {
	arr := ^Signal(signals.get(name))
	if arr == null {
		return
	}

	for c in arr^ {
		c(ctx)
	}
}
