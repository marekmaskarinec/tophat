
import (
	"th.um"
	"ui.um"
	"image.um"
)

// An element containing an image
type Image* = struct {
	g: ui.Generic
	image: image.Image
	stretch: bool
}

fn mk*(i: image.Image): Image {
	return Image {
		g: ui.Generic{},
		image: i}
}

fn (i: ^Image) get*(): ^ui.Generic {
	return &i.g
}

fn (i: ^Image) handle*() {
	i.g.handle()
}

fn (i: ^Image) draw*() {
	dm := i.image.getDims()
	s := th.Vf2{}
	if i.stretch {
		s.x = i.g.t.s.x/dm.x
		s.y = i.g.t.s.y/dm.y
	} else {
		if i.g.t.s.x < i.g.t.s.y {
			s.x = i.g.t.s.x/dm.x
			s.y = s.x
			i.g.t.p.y += (i.g.t.s.y - s.y*dm.y)/2
		} else {
			s.y = i.g.t.s.y/dm.y
			s.x = s.y
			i.g.t.p.x += (i.g.t.s.x - s.x*dm.x)/2
		}
	}

	i.image.draw(th.Transform{p: i.g.t.p, s: s}, th.white)
}

