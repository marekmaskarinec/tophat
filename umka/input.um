//~~
// Module for getting keyboard and mouse input.
// is* functions return info based on a us QWERTY layout. They are supposed to
// be used for game controls. For text input use getStr.
//~~

import (
	"th.um"
	"rect.um"
)

//~~Keycode constants
const (
	mouse1* =        char(1)
	mouse2* =        char(2)
	mouse3* =        char(3)
	mouse_up* =      char(4)
	mouse_down* =    char(5)
	key_shift* =     char(16   + 0x7f)
	key_backspace* = char(8    + 0x7f)
	key_delete* =    char(46   + 0x7f)
	key_left* =      char(37   + 0x7f)
	key_right* =     char(39   + 0x7f)
	key_up* =        char(38   + 0x7f)
	key_down* =      char(40   + 0x7f)
	key_escape* =    char(27   + 0x7f)
	key_enter* =     char(13   + 0x7f)	
	key_alt* =       char(0x12 + 0x7f)
	key_ctrl* =      char(0x11 + 0x7f)
	key_tab* =       char(0x09 + 0x7f)
	key_pg_down* =   char(0x22 + 0x7f)
	key_pg_up* =     char(0x21 + 0x7f)
	key_home* =      char(0x24 + 0x7f)
	key_end* =       char(0x23 + 0x7f)
	key_insert* =    char(0x2d + 0x7f)
	// F1 = key_fn+1, F2 = key_fn+2 etc.
	key_fn* =        char(0x70 - 1)
)
//~~

fn umth_input_get_mouse(out: ^th.Vf2)
//~~fn getMousePos
// Returns the position of mouse cursor in relation to the screen.
fn getMousePos*(): th.Vf2 {
//~~
	out := th.Vf2{}
	umth_input_get_mouse(&out)
	return out
}

//~~fn getGlobalMousePos
// Returns the position of mouse cursor in relation to cam.
fn getGlobalMousePos*(cam: rect.Rect): th.Vf2 {
//~~
	return cam.toWorld(getMousePos())
}

fn umth_input_is_pressed(keycode: char): int32
//~~fn isPressed
// Returns true if key is pressed. Either use codes defined in the file above,
// or pass lower case char/number.
fn isPressed*(code: char): bool {
//~~
	val := bool(umth_input_is_pressed(code))
	return val
}

fn umth_input_is_just_pressed(key: char): int32
//~~fn isJustPressed
// Returns, whether code was just pressed this loop.
fn isJustPressed*(code: char): bool {
//~~
	return bool(umth_input_is_just_pressed(code))
}

fn umth_input_is_just_released(key: char): int32
//~~fn isJustReleased
// Returns true if a key was just released.
fn isJustReleased*(code: char): bool {
//~~
	return bool(umth_input_is_just_released(code))
}

fn umth_input_clear*(code: char)
//~~fn clear
// Clears both the pressed and justPressed state of a code.
fn clear*(code: char) {
//~~
	umth_input_clear(code)
}

fn umth_input_get_str(): str
//~~fn getStr
// Returns a string entered by the user in the last cycle.
fn getStr*(): str {
//~~
	return umth_input_get_str()
}

fn umth_input_get_mouse_delta(o: ^th.Vf2)
//~~fn getMousDelta
// Returns the difference between mouse positions in the last cycle. Will work
// even if `window.freezeCursor` is enabled.
fn getMouseDelta*(): th.Vf2 {
//~~
	o := th.Vf2{}
	umth_input_get_mouse_delta(&o)
	return o
}
