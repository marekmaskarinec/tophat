import (
	"canvas.um"
	"font.um"
	"image.um"
	"input.um"
	"rect.um"
	"signal.um"
	"std.um"
	"th.um"
	"window.um"	
)

var (
	logo: image.Image
	fg: uint32
	bg: uint32
	lines: []str
	loadedFont: font.Font
)

fn onFrame(args: []any) {
	canvas.drawRect(bg, rect.mk(0, 0, window.wp.x, window.wp.y))

	p := th.Vf2{ 0, 1 }

	if !input.isPressed(input.key_left_alt) {
		for i in lines {
			s := 1
			if i == 1 {
				s = 2
			}

			canvas.drawText(
				lines[i],
				th.Vf2{
					(window.wp.x - canvas.textSize(lines[i], s).x) / 2,
					p.y},
				fg, s)
			p.y += 6 * s
		}
	} else {
		for i in lines {
			s := 1*(1/4.0)
			if i == 1 {
				s = 2*(1/4.0)
			}

			loadedFont.draw(
				lines[i],
				th.Vf2{
					(window.wp.x - loadedFont.measure(lines[i]).x * s) / 2,
					p.y},
				fg, s)
			p.y += 16 * s
		}
	}


	if input.isJustPressed(input.key_enter) {
		if th.platform == th.PlatformWindows {
			std.system("explorer \"https://th.mrms.cz/\"")
		} else if th.platform == th.PlatformLinux {
			std.system("xdg-open https://th.mrms.cz/")
		}
	}

	const s = 1.5
	logo.draw(th.Transform{
		p: p.add(window.wp.sub(p).sub(logo.getDims().mulf(s)).divf(2)),
		s: th.vf2f(s)})
}

fn init*() {
	window.setup("Hello world", 600, 600)
	window.setViewport(th.Vf2{ 100, 100 })
	
	loadedFont = font.load("etc/roboto.ttf", 16, 2)
	if !loadedFont.validate() {
		error("could not load font")
	}

	logo = image.load("etc/logo/logo-normal-white.png")

	fg = 0xadadd5ff
	bg = 0x262626ff

	lines = []str {
		"WELCOME TO",
		"TOPHAT",
		"",
		"EXAMPLES ARE IN",
		"THE examples/",
		"DIRECTORY",
		"",
		"PRESS ENTER TO",
		"OPEN THE",
		"HOMEPAGE" }

	window.onFrame.register(onFrame)
}

