import (
	"std.um"
	"input.um"
	"th.um"
	"rect.um"
	"image.um"
	"window.um"	
	"canvas.um"
	"font.um"
)

var (
	logo: image.Image
	fg: uint32
	bg: uint32
	lines: []str
	loadedFont: font.Font
)

fn onFrame(cctx, uctx: any) {
	canvas.drawRect(bg, rect.mk(0, 0, window.wp.x, window.wp.y))

	p := th.Vf2{ 0, 1 }
	for i in lines {
		s := 1*(1/4.0)
		if i == 1 {
			s = 2*(1/4.0)
		}

		loadedFont.draw(
			lines[i],
			th.Vf2{
				(window.wp.x - canvas.textSize(lines[i], s).x) / 2,
				p.y},
			fg, s)
		p.y += 16 * s
	}

	if input.isJustPressed(input.key_enter) {
		if th.platform == "windows" {
			std.system("open https://th.mrms.cz/")
		} else {
			std.system("xdg-open https://th.mrms.cz/")
		}
	}

	const s = 1.5
	logo.draw(th.Transform{
		p: p.add(window.wp.sub(p).sub(logo.getDims().mulf(s)).divf(2)),
		s: th.vf2f(s)})
}

fn init() {
	window.setup("Hello world", 600, 600)
	window.setViewport(th.Vf2{ 100, 100 })
	
	loadedFont = font.load("etc/roboto.ttf", 16, 2)
	if !loadedFont.validate() {
		error("could not load font")
	}

	logo = image.load("etc/logo/logo-normal-white.png")

	fg = 0xadadd5ff
	bg = 0x262626ff

	lines = []str {
		"WELCOME TO",
		"TOPHAT",
		"",
		"EXAMPLES ARE IN",
		"THE examples/",
		"DIRECTORY",
		"",
		"PRESS ENTER TO",
		"OPEN THE",
		"HOMEPAGE" }

	window.onFrame.register(onFrame, null)
}

fn main() { }