
import (
	"th.um"
	"hud.um"
	"ent.um"
	"input.um"
	"sound.um"
	"image.um"
	"enemy.um"
	"global.um"
	"effects.um"
)

const (
	speed = 0.07
	bulletSpeed = 0.1
	shootTimeout = 300
)

var (
	e*: ent.Ent
	bulletPrefab: ent.Ent
	bullets*: []ent.Ent
	frames: []image.Image
	lastShot: int = -shootTimeout
)

fn init*() {
	frames = []image.Image{
		image.load("gfx/ship1.png"),
		image.load("gfx/ship2.png")}

	e = ent.mk(
		frames[0],
		th.Transform{p: th.Vf2{y: global.cam.h/3}, s: th.Vf2{1, 1}})

	bulletPrefab.t.s = th.Vf2{1, 1}
	bulletPrefab.r.w = 1
	bulletPrefab.r.h = 2
	bulletPrefab.c = 0xAEAE0Cff

	bullets = []ent.Ent{}
}

fn handle*() {
	if input.isPressed(int32('a')) {
		e.t.p.x -= speed * th.delta
		if e.t.p.x < -global.cam.w/2 - e.r.w {
			e.t.p.x = global.cam.w/2
		}
	}
	if input.isPressed(int32('d')) {
		e.t.p.x += speed * th.delta
		if e.t.p.x > global.cam.w/2 {
			e.t.p.x = -global.cam.w/2 - e.r.w
		}
	}

	e.animate(4, &frames, th.time)

	if input.isJustPressed(input.KEY_SPACE) && th.time - lastShot >= shootTimeout {
		sound.shoot.play()
		b := bulletPrefab
		b.t.p = th.Vf2{e.t.p.x + 1, e.t.p.y - bulletPrefab.r.h}

		bullets = append(bullets, b)
		b.t.p.x += 3
		bullets = append(bullets, b)

		lastShot = th.time
	}

	enemies := enemy.getEnts()

	for i:=0; i >= 0 && i < len(bullets); i++ {
		bullets[i].t.p.y -= bulletSpeed * th.delta

		if bullets[i].t.p.y < -global.cam.h/2 {
			bullets = delete(bullets, i)
			i--
			continue
		}

		var ic: th.Vf2
		if colls := bullets[i].getColl(enemies, 1); len(colls) != 0 {
			bullets = delete(bullets, i)
			effects.enemyDestroy(enemy.enemies[colls[0].index].ent.t.p)
			enemy.enemies = delete(enemy.enemies, colls[0].index)
			enemies = enemy.getEnts()
			global.score++
			sound.explosion.play()
			i--
			continue
		}
	}
}

fn draw*() {
	e.draw(global.cam)

	for i:=0; i < len(bullets); i++ {
		bullets[i].draw(global.cam)
	}
}
