
import (
	"th.um"
	"std.um"
	"lerp.um"
	"global.um"
	"canvas.um"
	"particles.um"
)

var (
	dmgEffect: lerp.Lerper
	emitters: []particles.Emitter
)

fn init*() {
	dmgEffect = lerp.Lerper{}
	emitters = []particles.Emitter{}
}

fn handle*() { }

fn damage*() {
	dmgEffect.enqueue(
		lerp.Item{0, 0xaa, 200, 0, false, null, fn(ctx: any){}})
	dmgEffect.enqueue(
		lerp.Item{0xaa, 0, 200, 0, false, null, fn(ctx: any){}})
}

fn enemyDestroy*(pos: th.Vf2) {
	e := particles.Emitter{
		pos: pos,
		dm: th.Vf2{0, 0},
		gravity: th.Vf2{0.1, 0.1},
		repeat: false,
		active: true,

		angle: th.Vf2{0, 360},
		lifetime: 400,
		lifetimeRandomness: 0.4,

		velocity: 0.8,
		velocityRandomness: 0.2,

		size: 0.2,
		sizeRandomness: 0.1,
		maxSize: 4,

		colors: []uint32{0xFFA500ff, 0xFFA500dd, 0xe9330caa, 0xe9330c44} }

	e.genParticles(th.time, 400, 0.5)

	emitters = append(emitters, e)
}

fn draw*() {
	if len(dmgEffect) != 0 {
		val := round(dmgEffect.byDelta(th.delta))
		if val < 0 { val = 0 }
		canvas.drawRect(0xaa000000 | val, global.cam)
	}

	for i:=0; i>=0 && i < len(emitters) && len(emitters) != 0; i++ {
		emitters[i].draw(global.cam, th.time)
		if !emitters[i].active {
			if len(emitters) == 1 {
				emitters = []particles.Emitter{}
				continue
			}
			emitters = delete(emitters, i)
			i--
		}
	}
}
