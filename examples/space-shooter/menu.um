
import (
	"global.um"
	"hud.um"
	"rect.um"
	"std.um"
	"th.um"
	"ui.um"
	"window.um"
)

var gui: ui.Gui

fn init*() {
	gui = ui.mk(rect.fromVf2({}, window.wp), ui.getDefaultStyle())
	gui.getStyle().containerBox.color = 0
	gui.getStyle().ftColor = th.white
	gui.getStyle().ft = hud.mainFont
	gui.getStyle().ftScale = 0.1
}

fn handle*() {
	layout := ui.LayoutFn{
		gui.stack({ padding: 2 })
		gui.box({
			dimension: 10,
			padding: 2 })

			gui.dupStyle()
			gui.getStyle().ftScale *= 4

				gui.label("SPACE", { centerX: true })
				gui.label("SHOOTER", { centerX: true })

			gui.popStyle()

			gui.pushRect({ h: 10 })

			gui.label(sprintf("%d : %d", global.score, global.hi),
				{ centerX: true })


		gui.popContainer()

		gui.dupStyle()
		gui.getStyle().ftColor = th.black

		gui.box({
			dimension: 10,
			padding: 2,
			dir: ui.BoxDirectionUp })

			if gui.qbutton("EXIT") {
				global.scene = global.NONE
			}

			if gui.qbutton("PLAY") {
				global.score = 0
				global.scene = global.GAME
			}

		gui.popContainer()

		gui.popStyle()

		gui.popContainer()
	}

	gui.eval(layout)
	gui.draw(layout)
}
