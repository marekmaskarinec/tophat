
import (
	"th.um"
	"std.um"
	"rect.um"
	"global.um"
	"canvas.um"
	"window.um"
)

const (
	starNum = 200
	speed = 0.04
)

var (
	stars: []rect.Rect
)

fn init*() {
	stars = make([]rect.Rect, starNum)

	for i:=0; i < len(stars); i++ {
		stars[i].y = -std.rand()%round(window.wp.y)
		stars[i].x = std.rand()%round(window.wp.x)

		stars[i].w = 1
		stars[i].h = 2
	}
}

fn handle*() {
	for i:=0; i < len(stars); i++ {
		s := speed
		if round(stars[i].x) % 3 == 0 {
			s *= 1.5
		}
		stars[i].y += s * th.delta

		if stars[i].y > window.wp.y {
			stars[i].y = -std.rand()%round(window.wp.y/8)
			stars[i].x = std.rand()%round(window.wp.x)
		}
	}
}

fn draw*() {
	for i:=0; i < len(stars); i++ {
		canvas.drawRect(0xeeeeeeff, stars[i])
	}
}
