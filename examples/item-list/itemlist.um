
import (
	"th.um"
	"ui.um"
	"rect.um"
	"image.um"
	"input.um"
	"canvas.um"
)

type Item* = struct {
	img: image.Image
	text: str
}

type ItemList* = struct {
	g: ui.Generic

	items: []Item
	limit: int
	offset: int
	selection: int

	renderer: ui.TextRenderer
	fontSize: th.fu
	clickToSelect: bool

	scrollBarWidth: th.fu
	scrollOffset: th.fu
}

fn (i: ^Item) draw(pos, dm: th.Vf2, fg, bg: uint32, fs: th.fu) {
	canvas.drawRect(bg, rect.mk(pos.x, pos.y, dm.x, dm.y))
	x := 1 * fs
	if i.img.validate() {
		i.img.draw(th.Transform{p: pos, s: th.Vf2{1, 1}}, th.white)
		x := i.img.getDims().x
	}

	canvas.drawText(i.text, th.Vf2{pos.x + x, pos.y + 1 * fs}, fg, fs)
}

fn (il: ^ItemList) get*(): ^ui.Generic {
	return &il.g
}

fn (il: ^ItemList) itemWidth(): th.fu {
	return (il.renderer.measure("A").y + 2) * il.fontSize
}

fn (il: ^ItemList) doesHover(): bool {
	mp := input.getMousePos()
	return (mp.x > il.g.r.x + il.scrollBarWidth && mp.x < il.g.r.x + il.g.r.w &&
		mp.y > il.g.r.y && mp.y < il.g.r.y + il.limit * il.itemWidth())
}

fn (il: ^ItemList) handleScrollBar() {
	iw := real(il.limit)/len(il.items) * il.itemWidth()
	mp := input.getMousePos()

	if !(mp.x > il.g.r.x && mp.y < il.g.r.x + il.scrollBarWidth &&
	   mp.y > il.g.r.y + il.offset * iw && mp.y < il.g.r.y + (il.offset + il.limit) * iw) {
		return
	}

	if input.isJustPressed(input.mouse1) {
		il.scrollOffset = mp.y - il.g.r.y - il.offset * iw
		return
	}

	if input.isPressed(input.mouse1) {
		il.offset = trunc((mp.y - il.g.r.y - il.scrollOffset) / iw)

		if il.offset < 0 {
			il.offset = 0
		}
		if il.offset > len(il.items) - il.limit {
			il.offset = len(il.items) - il.limit
		}
	}
}

fn (il: ^ItemList) handle*() {
	il.handleScrollBar()

	mp := input.getMousePos()

	if !il.doesHover() {
		il.selection = -1
		il.g.pressed = false
	} else {
		if input.isJustPressed(input.key_up) && il.offset > 0 {
			il.offset--
		}
    
		if input.isJustPressed(input.key_down) && il.offset < len(il.items) - il.limit {
			il.offset++
		}

		if !il.clickToSelect || input.isJustPressed(input.mouse1) {
			il.selection = trunc((mp.y - il.g.r.y) / il.itemWidth() + il.offset)
			il.g.onEvent(ui.eventHover, il.g.ctx)
		}

		if input.isPressed(input.mouse1) {
			il.g.pressed = true
		} else if il.g.pressed {
			il.g.onEvent(ui.eventRelease, il.g.ctx)
			il.g.pressed = false
		}
	}
}

fn (il: ^ItemList) drawScrollBar() {
	iw := real(il.limit)/len(il.items) * il.itemWidth()

	canvas.drawRect(th.white, rect.mk(
		il.g.r.x, il.g.r.y + iw * il.offset,
		il.scrollBarWidth, iw * il.limit))	
}

fn (il: ^ItemList) draw*() {
	il.drawScrollBar()

	p := il.g.r.getPos()

	for i:=il.offset; i < il.offset + il.limit && i < len(il.items); i++ {
		fg := th.black
		bg := th.white
		if i == il.selection {
			fg = th.white
			bg = th.black
		}

		il.items[i].draw(th.Vf2{p.x + il.scrollBarWidth, p.y},
			th.Vf2{il.g.r.w, il.itemWidth()},
			fg, bg, il.fontSize)
		p.y += 7 * il.fontSize
	}
}
