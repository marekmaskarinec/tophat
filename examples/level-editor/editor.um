
import (
	"ui.um"
	"tilemap.um"
	"image.um"
	"input.um"
	"misc.um"
	"rawdraw.um"

	"global.um"
)

var (
	brushvis: ui.box // box, that shows, which tile is active
	buttons: []ui.button // all image buttons
	target: tilemap.tmap // the tilemap, that is editer
	mainbox: ui.box // box containing all ui
	brush: int32 // current tile
	images: []image.img // array of all tiles
)

// custom stepify function, that works better in this case
fn stepify(val, step: real): real {
	if step != 0 {
		val = round(val / step + 0.5) * step
	}
	return val
}

fn init*() {
	images = []image.img{image.load("1.png"), image.load("2.png"), image.load("3.png"), image.load("4.png")} // loading images

	// here we are making a tilemap by hand
	target = tilemap.tmap{
		images,
		72,
		0,
		20,
		make([]int32, 20*20),
		[]bool{false, false, false, false},
		6,
		tilemap.TOPLEFT}
	
	brushvis = ui.mkbox([]ui.element{}, false) // making brush visualizer
	brushvis.imagemode = ui.SCALE // setting it's image scaling mode to SCALE

	mainbox = ui.mkbox([]ui.element{&brushvis}, false) // creating mainbox

	// buttons
	buttons = make([]ui.button, len(images))
	for i:=0; i < len(images); i++ {
		buttons[i] = ui.mkbutton(ui.mklabel("", 1, 0xff))
		buttons[i].label.bgcolor = 0x33
		mainbox.children = append(mainbox.children, ui.mkbox([]ui.element{&buttons[i]}, false))
		^ui.box(mainbox.children[i+1]).image = images[i]
	}

	mainbox.padding.w = 120
	ui.mainelement = mainbox

	brush := 1
}

// this function paints a tile to target
fn paint*(tile: int) {
	x, y := input.getmousepos()
	x = round(stepify(x-target.x, target.cellsize)/target.cellsize)-1
	y = round(stepify(y-target.y, target.cellsize)/target.cellsize)-1

	if x < 0 || y < 0 {
		return
	}

	if y*target.w + x >= len(target.cells) { return }

	target.cells[y*target.w + x] = tile
}

fn run*() {
	// going through all the buttons. if it is pressed, we will set it's number as brush
	for i:=0; i < len(buttons); i++ {
		if buttons[i].pressed {
			brush = i + 1
		}
	}

	// setting visualizer's image
	if brush != 0 {
		brushvis.image = images[brush-1]
	}

	// click handling
	if input.ispressed(input.LMB) {
		paint(brush)
	}

	if input.ispressed(input.RMB) {
		paint(0) // zero means no tile
	}

	target.draw(global.cam) // drawing the tilemap

	// drawing the cursor
	x, y := input.getmousepos()
	x = round(stepify(x, target.cellsize)-target.cellsize-1)
	y = round(stepify(y, target.cellsize)-target.cellsize-1)
	rawdraw.setcolor(0x00ffffaa)
	rawdraw.drawrect(x, y, 6, 6)

	ui.handle(global.cam.w, global.cam.h) // handling ui
}
